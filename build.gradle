buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:0.9.19'
        classpath "com.netflix.nebula:gradle-ospackage-plugin:2.2.6"
        classpath "com.softwhistle:database-migrations-gradle-plugin:1.0.0-SNAPSHOT"
        classpath 'org.postgresql:postgresql:9.4.1209'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "migrations"
apply plugin: "nebula.os-package"
apply plugin: 'io.ratpack.ratpack-groovy'

group = 'com.softwhistle'
version = '1.0.0-SNAPSHOT'
mainClassName = 'com.softwhistle.Application'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'finances'
    }
}

compileJava {
    if (System.getProperty("compilerWarnings") != null) {
        options.compilerArgs = ['-Xlint:unchecked']
    }
}

task copyDependencies(type: Copy) {
    into "${buildDir}/distributions/dependencies/libs"
    from configurations.compile
    from configurations.runtime
}

task copyDeploy(type: Copy) {
    into project.buildDir
    from(file('src/deploy')) {
        into 'resources/deploy'
    }
}

processResources.dependsOn copyDeploy

dependencies {
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
    compile group: 'com.softwhistle', name: 'jdbc-operations', version: '1.0.0-SNAPSHOT'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
    runtime group: 'org.postgresql', name: 'postgresql', version: '9.4.1209'
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

migrations {
    superUrl = 'jdbc:postgresql://whistletoon.civoso4i1xz9.us-west-2.rds.amazonaws.com:5432/postgres'
    url = 'jdbc:postgresql://whistletoon.civoso4i1xz9.us-west-2.rds.amazonaws.com:5432/finances'
    database = 'finances'
    driverClassName = 'org.postgresql.Driver'
    username = 'professorfalkin'
    password = 'hellojoshua'
    baseDir = 'src/main/database/migrations'
    // if normal user is not superuser capable of creating/dropping databases
    // superUsername = 'professorfalkin'
    // superPassword = 'joshua'
}

buildRpm
{
    dependsOn 'copyDependencies'

    packageName = 'swistle-finman'
    version = project.version.replaceAll('\\-SNAPSHOT', '')
    release = 1
    os = 'LINUX'
    arch = 'noarch'
    createDirectoryEntry = true
    
    def baseInstall = '/opt/softwhistle/finances-webservice'
    
    into baseInstall

    directory('/var/log/softwhistle')

    from(new File(project.buildDir, 'distributions/dependencies/libs')) {
        into('libs')
    }
    from(new File(project.buildDir, 'classes/main')) {
        into('classes')
    }
    from(new File(project.buildDir, 'resources/main')) {
        into('classes')
    }
    from(new File(project.buildDir, 'resources/deploy/bin')) {
        into('bin')
    }
    
    link("/etc/init.d/$packageName", "$baseInstall/bin/etc/init.d")
}
